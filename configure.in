dnl $Id$

AC_PREREQ(2.54)
AC_INIT(Makefile.am)

AC_ARG_PROGRAM

FOE_VERSION_MAJOR=0
FOE_VERSION_MINOR=1
FOE_VERSION=$FOE_VERSION_MAJOR.$FOE_VERSION_MINOR
AC_SUBST(FOE_VERSION)

AC_CONFIG_AUX_DIR(config)
AM_CONFIG_HEADER(config/config.h)
AM_INIT_AUTOMAKE(foe, $FOE_VERSION)

AC_CONFIG_SUBDIRS(3rdparty/cppunit 3rdparty/zlib)

dnl Check for programs.
AC_PROG_CC
AC_PROG_CXX

AC_PROG_LIBTOOL
AC_SUBST(LIBTOOL_DEPS)

AC_CHECK_PROG(RC, windres, windres)
AM_CONDITIONAL(HAVE_RC, test "$RC" = "windres")

AC_OBJEXT
AC_EXEEXT

if test "$enable_static" = "yes"; then
   CXXFLAGS="$CXXFLAGS -DSTATIC_BUILD"
fi;

AC_ARG_ENABLE(debug,
              AC_HELP_STRING([--enable-debug], [enable debug information]),
              CXXFLAGS="$CXXFLAGS -ggdb -DDEBUG -D_DEBUG",
              CXXFLAGS="$CXXFLAGS -O2 -DNDEBUG")

AC_SUBST(CXXFLAGS)

CORE_LIBS=
case $build in
   *cygwin*|*mingw*)
      CXXFLAGS="$CXXFLAGS -mwindows"
      CORE_LIBS="-lwinmm -lgdi32 -lopengl32"
      ;;
esac
AC_SUBST(CORE_LIBS)

dnl Check for libraries
AC_PATH_XTRA
AM_CONDITIONAL(NO_X, test "$no_x" = "yes")

MDL_HAVE_OPENGL

dnl The Win32 API headers in the cygwin environment typedefs REFGUID 
dnl as a pointer. The Win32 Platform SDK typedefs it as an actual
dnl reference.
AC_MSG_CHECKING(for actual REFGUID type)
AC_TRY_COMPILE([#include <winnt.h>],
               [GUID g; REFGUID rg = g;],
               refguid_is_reference=yes,
               refguid_is_reference=no)
if test "$refguid_is_reference" = yes; then
   AC_MSG_RESULT(reference)
else
   AC_MSG_RESULT(pointer)
   AC_DEFINE(REFGUID_IS_POINTER, 1, [REFGUID is actually typedef'ed as pointer-to-GUID])
fi

AC_OUTPUT([Makefile 3rdparty/Makefile 3rdparty/tinyxml/Makefile game/Makefile render/Makefile tech/Makefile techguid/Makefile])
